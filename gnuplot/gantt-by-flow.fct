#!/usr/bin/env gnuplot
set style fill transparent solid 0.3 border lc black
set style arrow 1 lc var filled size screen 0.03,15,90
set style arrow 2 lc black empty size screen 0.03,15,90
set style arrow 3 lc black nohead
set style arrow 4 lc black nohead
set key noautotitle

SEP = ' '

flows = ''
flow_append(d) = strstrt(flows, strcol(d).SEP) ? '' : flows=flows.SEP.strcol(d).SEP
flow_id(w) = words(substr(flows, 0, strstrt(flows, strcol(w).SEP)-1))
flow = '(flow_id(1))'  # used as macro

links = ''
link_append(d) = strstrt(links, strcol(d).SEP) ? '' : links=links.SEP.strcol(d).SEP
link_id(w) = words(substr(links, 0, strstrt(links, strcol(w).SEP)-1))
link = '(link_id(4))'  # used as macro


boxylow  = '(@flow-($3+1)*boxheight)'
boxyhigh = '(@flow-($3+0)*boxheight)'


gantt = " \
    u (flow_append(1), link_append(4), NaN):@flow:ytic(1) lc bgnd, \
 '' u 6:@flow:7:8:@boxylow:@boxyhigh:@link+1           with boxxyerror lc var, \
 '' u 6:@boxylow:(0):(boxheight+boxovershootl):@link+1 with vectors as 1, \
 '' u 6:@boxylow:(0):(boxheight+boxovershootl)         with vectors as 2, \
 '' u 6:@boxylow:($8-$6):(0)                           with vectors as 3, \
 '' u ($9==$8?$9:NaN):@boxyhigh:(0):(boxovershootr)    with vectors as 4, \
 for [i=1:words(links)] NaN with boxes lc i title word(links, i)"
